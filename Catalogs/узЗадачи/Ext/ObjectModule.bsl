
Функция ПроверитьПриоритетИсполнителя()
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	//С приоритетом 100 можно создавать множество задач
	Если ПриоритетИсполнителя = 100 Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Исполнитель", Исполнитель);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПриоритетИсполнителя", ПриоритетИсполнителя);
	Запрос.УстановитьПараметр("ВидСтатуса", Справочники.узВидыСтатусов.ВРаботе);
	
	// 11.05.18 Рудаков Добавлено в запрос "Разрешенные"
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ узЗадачи.Ссылка) КАК КоличествоЗадач
	|ИЗ
	|	Справочник.узЗадачи КАК узЗадачи
	|ГДЕ
	|	узЗадачи.Исполнитель = &Исполнитель
	|	И узЗадачи.ПриоритетИсполнителя = &ПриоритетИсполнителя
	|	И НЕ узЗадачи.Ссылка = &Ссылка
	|	И узЗадачи.Статус.ВидСтатуса = &ВидСтатуса"
	;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.КоличествоЗадач > 0 Тогда
			Сообщить("У исполнителя уже есть задачи с приоритетом " + ПриоритетИсполнителя + " - " + Выборка.КоличествоЗадач + "шт.");
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;	
	КонецЕсли; 
	
	Отказ = ПроверитьПриоритетИсполнителя();
	
	Если Отказ Тогда
		Возврат;
	Конецесли;
	
	Если ДополнительныеСвойства.Свойство("узЭтоОбработка") Тогда
		Возврат;
	Конецесли;
		
	Если НЕ ЗначениеЗаполнено(ДатаСоздания) Тогда
		ДатаСоздания = ТекущаяДата();		
	Конецесли;
	Если НЕ ЗначениеЗаполнено(ДатаНачалаРаботПоЗадаче) Тогда
		ДатаНачалаРаботПоЗадаче = ДатаСоздания;		
	Конецесли;	
	
	//Если ЭтоНовый()
	//	И НЕ ЗначениеЗаполнено(Родитель) Тогда
	//	ЭтоОсновнаяЗадача = Истина;
	//Конецесли;
	
	//ОсновнаяЗадача = ПолучитьОсновнуюЗадачу(Родитель);
	
	ЗаполнитьСтатусЗадачиЕслиОнНеЗаполнен(Отказ);
	
	РезультатПроверкиWIPЛимит = ПроверитьWIPЛимит();
	Если РезультатПроверкиWIPЛимит.ПревышенWIPЛимит Тогда
		Отказ = Истина;
		Сообщить(РезультатПроверкиWIPЛимит.ТекстОшибки);
		Возврат;
	Конецесли;		
	
	пЕстьПодчиненныеЗадачи = ЕстьПодчиненныеЗадачи(Ссылка);	
	
	УказаноСвойство_узЭтоЗаписьРодителя = ДополнительныеСвойства.Свойство("узЭтоЗаписьРодителя");
	
	Если НЕ УказаноСвойство_узЭтоЗаписьРодителя Тогда
		ЕстьПодчиненныеЗадачи = пЕстьПодчиненныеЗадачи;
	Конецесли;	
	
	Если пЕстьПодчиненныеЗадачи
		И НЕ УказаноСвойство_узЭтоЗаписьРодителя Тогда
		ИзменитьОсновнуюЗадачуДляВсехПодчиненныхЗадач();
	Конецесли;
	
	//НовыйРодитель = Родитель;
	//Если ЗначениеЗаполнено(НовыйРодитель)
	//	И НЕ НовыйРодитель.ЕстьПодчиненныеЗадачи Тогда
	//	НовыйРодительОбъект = НовыйРодитель.ПолучитьОбъект();
	//	НовыйРодительОбъект.ЕстьПодчиненныеЗадачи = Истина;
	//	НовыйРодительОбъект.ДополнительныеСвойства.Вставить("узЭтоЗаписьРодителя");
	//	НовыйРодительОбъект.Записать();
	//Конецесли;
	//Если НЕ ЭтоНовый() Тогда
	//	СтарыйРодитель = Ссылка.ПолучитьОбъект().Родитель;
	//	Если СтарыйРодитель <> НовыйРодитель Тогда
	//		пЕстьПодчиненныеЗадачи = ЕстьПодчиненныеЗадачи(СтарыйРодитель,Ссылка);
	//		Если ЗначениеЗаполнено(СтарыйРодитель)
	//			И пЕстьПодчиненныеЗадачи <> СтарыйРодитель.ЕстьПодчиненныеЗадачи Тогда
	//			СтарыйРодительОбъект = СтарыйРодитель.ПолучитьОбъект();
	//			СтарыйРодительОбъект.ЕстьПодчиненныеЗадачи = пЕстьПодчиненныеЗадачи;
	//			СтарыйРодительОбъект.ДополнительныеСвойства.Вставить("узЭтоЗаписьРодителя");
	//			СтарыйРодительОбъект.Записать();
	//		Конецесли;
	//	Конецесли;
	//Конецесли;	
	
	ТекПользователь = Пользователи.ТекущийПользователь();
	ДатаПоследнегоИзменения = ТекущаяДата();
	АвторПоследнегоИзменения = ТекПользователь;

	СтарыйСтатус = Ссылка.Статус;
	
	СтарыйВидСтатуса = СтарыйСтатус.ВидСтатуса;
	НовыйВидСтатуса = Статус.ВидСтатуса;
	ВидСтатуса_Готово = ПредопределенноеЗначение("Справочник.узВидыСтатусов.Готово");
	ВидСтатуса_ВРаботе = ПредопределенноеЗначение("Справочник.узВидыСтатусов.ВРаботе");
	ВидСтатуса_Релиз = ПредопределенноеЗначение("Справочник.узВидыСтатусов.Релиз");
	Статус_НаПаузе = Справочники.узСтатусыЗадачи.НайтиПоНаименованию("На паузе");
	
	Если НовыйВидСтатуса = ВидСтатуса_Готово
		И СтарыйВидСтатуса <> ВидСтатуса_Готово Тогда
		ДатаВыполнения = ТекущаяДата();	
	Конецесли;
	Если НовыйВидСтатуса <> ВидСтатуса_Готово Тогда
		Если ЗначениеЗаполнено(ДатаВыполнения) Тогда
			ДатаВыполнения = Неопределено; 
		Конецесли;			
	Конецесли;
	Если НовыйВидСтатуса = ВидСтатуса_ВРаботе
		ИЛИ НовыйВидСтатуса = ВидСтатуса_Готово Тогда
		Если НЕ ЗначениеЗаполнено(Исполнитель) Тогда
			Исполнитель = ТекПользователь; 
		Конецесли;
	Конецесли;
	Если НовыйВидСтатуса = ВидСтатуса_Релиз Тогда
		
		ФД_РезультатПользователь = РезультатПользователь.Получить();
		ФД_РезультатПрограммист = РезультатПрограммист.Получить();
		
		Попытка
			Если Не ЗначениеЗаполнено(ФД_РезультатПользователь.ПолучитьТекст()) или Не ЗначениеЗаполнено(ФД_РезультатПрограммист.ПолучитьТекст()) Тогда
				 Отказ = Истина;
				 Сообщить("Не заполнен результат!");
			КонецЕсли;
		Исключение
			Сообщить("Не заполнен результат!");
		КонецПопытки;
	КонецЕсли;
	Если Статус = Статус_НаПаузе Тогда
		
		Если Не ЗначениеЗаполнено(Комментарий) Тогда
			 Отказ = Истина;
			 Сообщить("Не заполнен комментарий при установке статуса На паузе!");
		КонецЕсли;
		
	КонецЕсли;
	
	ИзмененЦветПоУмолчанию = ПолучитьИзмененЦветПоУмолчанию();
	
	Если ФактическиеЧасы.Количество() > 0 Тогда
		ЧасыФакт = ФактическиеЧасы.Итог("ЧасыФакт");
	Конецесли;
	
	СобытияВИстории = ОбновитьИсторию();
	
	ДополнительныеСвойства.Вставить("СобытияВИстории",СобытияВИстории);
	
	//agroeco+ miv
	ПредметСтрокой = ОбщегоНазначения.ПредметСтрокой(Предмет);
	Если Статус.ВидСтатуса = Справочники.узВидыСтатусов.Релиз И Не ЗначениеЗаполнено(Конфигурация) Тогда
		Отказ = Истина;
		Сообщить("При передаче в релиз необходимо указать конфигурацию!");
		Возврат;
	КонецЕсли;
	
	//Проверка 
	//agroeco- miv
	
КонецПроцедуры

Процедура ЗаполнитьСтатусЗадачиЕслиОнНеЗаполнен(Отказ)
	Перем пНовыйСтатус;
	
	Если ЗначениеЗаполнено(Статус) Тогда
		Возврат;
	Конецесли;	
	
	пНовыйСтатус = ПредопределенноеЗначение("Справочник.узСтатусыЗадачи.Зарегистрирована");
	
	СтатусЗадачиПоУмолчанию = Справочники.узКонстанты.ПолучитьЗначениеКонстанты(
		"СтатусЗадачиПоУмолчанию",
		Тип("СправочникСсылка.узСтатусыЗадачи"),
		Ложь,Ложь);
		
	Если ЗначениеЗаполнено(СтатусЗадачиПоУмолчанию) Тогда
		пНовыйСтатус = СтатусЗадачиПоУмолчанию;		
	Конецесли;	
		
	Статус = пНовыйСтатус;
КонецПроцедуры //ЗаполнитьСтатусЗадачи(Отказ)

Функция ПолучитьИзмененЦветПоУмолчанию() Экспорт
	
	пИзмененЦветПоУмолчанию = Ложь;
	
	ЦветУЗадачи = ЦветЗадачи.Получить();
	ЦветПоУмолчанию = Новый Цвет(0,0,0);
	
	Если ЦветУЗадачи <> ЦветПоУмолчанию Тогда
		пИзмененЦветПоУмолчанию = Истина;	
	Конецесли;
	
	Возврат пИзмененЦветПоУмолчанию;	
КонецФункции

Функция ОбновитьИсторию()
	СтарыйИсполнитель = Ссылка.Исполнитель;
	СтарыйСтатус = Ссылка.Статус;
	СтарыйСрокИсполнения = Ссылка.СрокИсполнения;
	
	СобытияВИстории = Новый Структура();
	СобытияВИстории.Вставить("СтарыйИсполнитель",СтарыйИсполнитель);
	СобытияВИстории.Вставить("СтарыйСтатус",СтарыйСтатус);
	
	ТЗСобытияВИсторииДляУведомлений = Новый ТаблицаЗначений;
	ТЗСобытияВИсторииДляУведомлений.Колонки.Добавить("ВидСобытия",Новый ОписаниеТипов("ПеречислениеСсылка.узВидыСобытий"));
	ТЗСобытияВИсторииДляУведомлений.Колонки.Добавить("МассивИзмененнийПоКомментариям",Новый ОписаниеТипов("Массив"));
	
	ДатаСобытия = ТекущаяДата();
	пАвтор = Пользователи.ТекущийПользователь();
	
	ПараметрыДляИстории = Новый Структура();
	ПараметрыДляИстории.Вставить("ДатаСобытия",ДатаСобытия);
	ПараметрыДляИстории.Вставить("Автор",пАвтор);
	ПараметрыДляИстории.Вставить("НовыйСтатус",Статус);
	
	Если ЭтоНовый() Тогда
		ВидСобытия = ПредопределенноеЗначение("Перечисление.узВидыСобытий.ДобавленаЗадача");
		
		Событие = узОбщийМодульСервер.ПолучитьТекстСообщения("Добавлена задача",9);
		
		ПараметрыДляИстории.Вставить("ВидСобытия",ВидСобытия);
		ПараметрыДляИстории.Вставить("Событие",Событие);
		ДобавитьВИсторию(ПараметрыДляИстории,ТЗСобытияВИсторииДляУведомлений);
		
		ДобавитьВИсториюСтатусов();
		СобытияВИстории.Вставить("ТЗСобытияВИсторииДляУведомлений",ТЗСобытияВИсторииДляУведомлений);
		Возврат СобытияВИстории;
	Конецесли;	
	
	
	Если Исполнитель <> СтарыйИсполнитель Тогда
		ВидСобытия = ПредопределенноеЗначение("Перечисление.узВидыСобытий.НовыйИсполнитель");
		Если НЕ ЗначениеЗаполнено(СтарыйИсполнитель) Тогда
			пТекстСообщения = узОбщийМодульСервер.ПолучитьТекстСообщения("Указан исполнитель [%1]",10);
			Событие = СтрШаблон(пТекстСообщения,Исполнитель);
		Иначе
			пТекстСообщения = узОбщийМодульСервер.ПолучитьТекстСообщения("Изменен исполнитель с [%1] на [%2]",11);
			Событие = СтрШаблон(пТекстСообщения,СтарыйИсполнитель,Исполнитель);
			
			Событие = "Изменен исполнитель с ["+СтарыйИсполнитель+"] на [" + Исполнитель+"]";
		Конецесли;
		
		ПараметрыДляИстории.Вставить("ВидСобытия",ВидСобытия);
		ПараметрыДляИстории.Вставить("Событие",Событие);
		ДобавитьВИсторию(ПараметрыДляИстории,ТЗСобытияВИсторииДляУведомлений);
		
		ДобавитьВИсториюСтатусов();
	Конецесли;
	
	СтарыйСтатус = Ссылка.Статус;
	Если Статус <> СтарыйСтатус Тогда
		ДобавитьВИсториюСтатусов();
		
		ВидСобытия = ПредопределенноеЗначение("Перечисление.узВидыСобытий.ИзменениеСтатуса");
		
		пТекстСообщения = узОбщийМодульСервер.ПолучитьТекстСообщения("Изменен статус с [%1] на [%2]",12);
		Событие = СтрШаблон(пТекстСообщения,СтарыйСтатус,Статус);
		
		ПараметрыДляИстории.Вставить("ВидСобытия",ВидСобытия);
		ПараметрыДляИстории.Вставить("Событие",Событие);
		ДобавитьВИсторию(ПараметрыДляИстории,ТЗСобытияВИсторииДляУведомлений);
	Конецесли;
	
	Если СрокИсполнения <> СтарыйСрокИсполнения Тогда
		
		ДобавитьВИсториюСроковИсполнения();
		
	КонецЕсли;
	
	ОбновитьИсториюПоКомментариям(ПараметрыДляИстории,ТЗСобытияВИсторииДляУведомлений);
	
	СтароеНаименование = СокрЛП(Ссылка.Наименование);
	СтароеТекстСодержания = СокрЛП(Ссылка.ТекстСодержания);
	Если СокрЛП(Наименование) <> СтароеНаименование
		ИЛИ СокрЛП(ТекстСодержания) <> СтароеТекстСодержания Тогда
		ВидСобытия = ПредопределенноеЗначение("Перечисление.узВидыСобытий.ИзмененоОписаниеЗадачи");
		
		Событие = узОбщийМодульСервер.ПолучитьТекстСообщения("Изменено описание задачи",13);
		
		ПараметрыДляИстории.Вставить("ВидСобытия",ВидСобытия);
		ПараметрыДляИстории.Вставить("Событие",Событие);
		ДобавитьВИсторию(ПараметрыДляИстории,ТЗСобытияВИсторииДляУведомлений);
	Конецесли;
	
	История.Сортировать("ДатаСобытия УБЫВ");
	
	СобытияВИстории.Вставить("ТЗСобытияВИсторииДляУведомлений",ТЗСобытияВИсторииДляУведомлений);
	Возврат СобытияВИстории;
	
	
КонецФункции

Процедура ОбновитьИсториюПоКомментариям(ПараметрыДляИстории,ТЗСобытияВИсторииДляУведомлений) 
	МассивИзмененнийПоКомментариям = Новый Массив();
	
	РезультатЗапроса = ОбновитьИсториюПоКомментариям_ПолучитьРезультатЗапроса();	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	Конецесли;
	
	Выборка = РезультатЗапроса.Выбрать();
	ЕстьИзмененныеКомментарии = Ложь;
	ЕстьДобавленныеКомментарии = Ложь;
	Пока Выборка.Следующий() Цикл
		ТекстИзменения = "";
		Если НЕ ЗначениеЗаполнено(Выборка.КлючСтрокиСтарый) Тогда
			пТекстСообщения = узОбщийМодульСервер.ПолучитьТекстСообщения("Новый комментарий: %1",14);
			ТекстИзменения = СтрШаблон(пТекстСообщения,Выборка.КомментарийНовый);
			
			ЕстьДобавленныеКомментарии = Истина;
		ИначеЕсли СокрЛП(Выборка.КомментарийСтарый) <> СокрЛП(Выборка.КомментарийНовый) Тогда 

			пТекстСообщения = узОбщийМодульСервер.ПолучитьТекстСообщения("Изменили комментарий, новый текст: %1",15);
			ТекстИзменения = СтрШаблон(пТекстСообщения,Выборка.КомментарийНовый);
			
			ЕстьИзмененныеКомментарии = Истина;
		Конецесли;		
		
		Если НЕ ЗначениеЗаполнено(ТекстИзменения) Тогда
			Продолжить;
		Конецесли;
		
		МассивИзмененнийПоКомментариям.Добавить(ТекстИзменения);
	КонецЦикла;
		
	Если ЕстьИзмененныеКомментарии Тогда
		ВидСобытия = ПредопределенноеЗначение("Перечисление.узВидыСобытий.ИзмененКомментарий");
		
		Событие = узОбщийМодульСервер.ПолучитьТекстСообщения("Изменили комментарии",16);
		
		ПараметрыДляИстории.Вставить("ВидСобытия",ВидСобытия);
		ПараметрыДляИстории.Вставить("Событие",Событие);
		ПараметрыДляИстории.Вставить("МассивИзмененнийПоКомментариям",МассивИзмененнийПоКомментариям);
		ДобавитьВИсторию(ПараметрыДляИстории,ТЗСобытияВИсторииДляУведомлений);
		
	ИначеЕсли ЕстьДобавленныеКомментарии Тогда
		ВидСобытия = ПредопределенноеЗначение("Перечисление.узВидыСобытий.ДобавленКомментарий");

		Событие = узОбщийМодульСервер.ПолучитьТекстСообщения("Добавлен новый комментарий",17);
		
		ПараметрыДляИстории.Вставить("ВидСобытия",ВидСобытия);
		ПараметрыДляИстории.Вставить("Событие",Событие);
		ПараметрыДляИстории.Вставить("МассивИзмененнийПоКомментариям",МассивИзмененнийПоКомментариям);
		ДобавитьВИсторию(ПараметрыДляИстории,ТЗСобытияВИсторииДляУведомлений);
	Конецесли;
КонецПроцедуры

Функция ОбновитьИсториюПоКомментариям_ПолучитьРезультатЗапроса() 
	ТЗСтарыеКомментарии = Ссылка.Комментарии;
	ТЗНовыеКомментарии = Комментарии;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	узЗадачиКомментарии.КлючСтроки,
	|	узЗадачиКомментарии.ДатаКомментария КАК ДатаКомментарияСтарый,
	|	узЗадачиКомментарии.Автор КАК АвторСтарый,
	|	узЗадачиКомментарии.Комментарий КАК КомментарийСтарый,
	|	узЗадачиКомментарии.Выполнено КАК ВыполненоСтарый
	|ПОМЕСТИТЬ ВТСтарыеКомментарии
	|ИЗ
	|	Справочник.узЗадачи.Комментарии КАК узЗадачиКомментарии
	|ГДЕ
	|	узЗадачиКомментарии.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТЗНовыеКомментарии.КлючСтроки,
	|	ТЗНовыеКомментарии.ДатаКомментария КАК ДатаКомментарияНовый,
	|	ТЗНовыеКомментарии.Автор КАК АвторНовый,
	|	ТЗНовыеКомментарии.Комментарий КАК КомментарийНовый,
	|	ТЗНовыеКомментарии.Выполнено КАК ВыполненоНовый
	|ПОМЕСТИТЬ ВТНовыеКомментарии
	|ИЗ
	|	&ТЗНовыеКомментарии КАК ТЗНовыеКомментарии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСтарыеКомментарии.КлючСтроки КАК КлючСтрокиСтарый,
	|	ВТСтарыеКомментарии.ДатаКомментарияСтарый,
	|	ВТСтарыеКомментарии.АвторСтарый,
	|	ВТСтарыеКомментарии.КомментарийСтарый,
	|	ВТСтарыеКомментарии.ВыполненоСтарый,
	|	ВТСтарыеКомментарии.КлючСтроки КАК КлючСтрокиНовый,
	|	ВТНовыеКомментарии.ДатаКомментарияНовый,
	|	ВТНовыеКомментарии.АвторНовый,
	|	ВТНовыеКомментарии.КомментарийНовый,
	|	ВТНовыеКомментарии.ВыполненоНовый
	|ИЗ
	|	ВТСтарыеКомментарии КАК ВТСтарыеКомментарии
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТНовыеКомментарии КАК ВТНовыеКомментарии
	|		ПО ВТСтарыеКомментарии.КлючСтроки = ВТНовыеКомментарии.КлючСтроки";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ТЗНовыеКомментарии", ТЗНовыеКомментарии);
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса;
КонецФункции 

Процедура ДобавитьВИсториюСроковИсполнения() Экспорт
	
	ДобавитьНовуюСтроку = Истина;
	ВсегоСтрок = ИсторияСроковИсполнения.Количество();
	Если ВсегоСтрок > 0 Тогда
		ПоследняяСтрокаИсторияСроковИсполнения = ИсторияСроковИсполнения[ВсегоСтрок-1];
		Если ПоследняяСтрокаИсторияСроковИсполнения.Исполнитель = Исполнитель 
			И ПоследняяСтрокаИсторияСроковИсполнения.СрокИсполнения = СрокИсполнения Тогда
			ДобавитьНовуюСтроку = Ложь;
		Конецесли;
	Конецесли;
	
	Если НЕ ДобавитьНовуюСтроку Тогда
		Возврат;
	Конецесли;
	
	СтрокаИсторияСроковИсполнения = ИсторияСроковИсполнения.Добавить();
	СтрокаИсторияСроковИсполнения.ДатаИзменения = ТекущаяДата();
	СтрокаИсторияСроковИсполнения.Исполнитель = Исполнитель;
	СтрокаИсторияСроковИсполнения.СрокИсполнения = СрокИсполнения;
КонецПроцедуры 

Процедура ДобавитьВИсториюСтатусов() Экспорт
	
	ДобавитьНовуюСтроку = Истина;
	ВсегоСтрок = ИсторияСтатусов.Количество();
	Если ВсегоСтрок > 0 Тогда
		ПоследняяСтрокаИсторияСтатусов = ИсторияСтатусов[ВсегоСтрок-1];
		Если ПоследняяСтрокаИсторияСтатусов.Исполнитель = Исполнитель 
			И ПоследняяСтрокаИсторияСтатусов.Статус = Статус Тогда
			ДобавитьНовуюСтроку = Ложь;
		Конецесли;
	Конецесли;
	
	Если НЕ ДобавитьНовуюСтроку Тогда
		Возврат;
	Конецесли;
	
	ПроставитьДатуОкончанияДляСтарогоСтатуса();	
	
	СтрокаИсторияСтатусов = ИсторияСтатусов.Добавить();
	СтрокаИсторияСтатусов.ДатаНачала = ТекущаяДата();
	СтрокаИсторияСтатусов.Исполнитель = Исполнитель;
	СтрокаИсторияСтатусов.Статус = Статус;
КонецПроцедуры 

Процедура ПроставитьДатуОкончанияДляСтарогоСтатуса()
	ВсегоСтрок = ИсторияСтатусов.Количество();
	Если ВсегоСтрок = 0 Тогда
		Возврат;
	Конецесли;
	
	СтрокаИсторияСтатусов = ИсторияСтатусов[ВсегоСтрок-1];
	СтрокаИсторияСтатусов.ДатаОкончания = ТекущаяДата();
КонецПроцедуры 

Процедура ДобавитьВИсторию(ПараметрыДляИстории,ТЗСобытияВИсторииДляУведомлений)
	
	СтрокаИстория = История.Добавить();
	СтрокаИстория.ДатаСобытия = ПараметрыДляИстории.ДатаСобытия;
	СтрокаИстория.Автор = ПараметрыДляИстории.Автор;
	СтрокаИстория.ВидСобытия = ПараметрыДляИстории.ВидСобытия;
	СтрокаИстория.Событие = ПараметрыДляИстории.Событие;
	
	Если ПараметрыДляИстории.НовыйСтатус = ПредопределенноеЗначение("Справочник.узСтатусыЗадачи.Архив") Тогда
		Возврат;
	Конецесли;
	
	СтрокаТЗСобытияВИсторииДляУведомлений = ТЗСобытияВИсторииДляУведомлений.Добавить();
	СтрокаТЗСобытияВИсторииДляУведомлений.ВидСобытия = ПараметрыДляИстории.ВидСобытия;
	СтрокаТЗСобытияВИсторииДляУведомлений.МассивИзмененнийПоКомментариям = Новый Массив();
	Если ПараметрыДляИстории.Свойство("МассивИзмененнийПоКомментариям") Тогда
		СтрокаТЗСобытияВИсторииДляУведомлений.МассивИзмененнийПоКомментариям = ПараметрыДляИстории.МассивИзмененнийПоКомментариям;
	Конецесли;
КонецПроцедуры 

Процедура ОтправитьУведомлениеНаПочту(СобытияВИстории)
	
	НастройкиСобытий = ПолучитьНастройкиСобытий();
	
	ОтправитьУведомлениеНаПочтуИсполнителю(НастройкиСобытий,СобытияВИстории);
	
	ОтправитьУведомлениеНаПочтуСтаромуИсполнителю(НастройкиСобытий,СобытияВИстории);
	
	ОтправитьУведомлениеНаПочтуНаблюдателям(НастройкиСобытий,СобытияВИстории);
	
	//////agroeco+, КлочковСВ
	ОтправитьУведомлениеНаПочтуИнициатору(НастройкиСобытий,СобытияВИстории);
	ОтправитьУведомлениеНаПочтуРуководителюИнициатора(НастройкиСобытий,СобытияВИстории);
	//////agroeco-
	
КонецПроцедуры 

//agroeco+, КлочковСВ
Процедура ОтправитьУведомлениеНаПочтуИнициатору(НастройкиСобытий,СобытияВИстории)
	
	Если Статус = СобытияВИстории.СтарыйСтатус Тогда
		Возврат;	
	КонецЕсли;
	
	Получатель = аэИнициатор.Пользователь;
	
	Если НЕ ЗначениеЗаполнено(Получатель) Тогда
		Возврат;
	Конецесли;
	
	Если Получатель.узНеОтправлятьУведомленияНаПочту Тогда
		Возврат;
	Конецесли;
	
	ТекПользователь = Пользователи.ТекущийПользователь(); 
	//Если ТекПользователь = Получатель Тогда			
	//	Возврат;
	//Конецесли;	
	ТЗСобытияВИсторииДляУведомлений = СобытияВИстории.ТЗСобытияВИсторииДляУведомлений;
	ВсегоСобытий = ТЗСобытияВИсторииДляУведомлений.Количество();
	Если ВсегоСобытий = 0 Тогда
		Возврат;
	Конецесли;
	
	ВТДопПараметры = Новый Структура();
	ВТДопПараметры.Вставить("ЭтоОтправкаИнициатору",Истина);
	ВТДопПараметры.Вставить("НастройкиСобытий",НастройкиСобытий);
	РезультатФункции = ПолучитьТемаПисьмаСобытие(ВТДопПараметры,СобытияВИстории);
	
	Если НЕ РезультатФункции.НеобходимоОтправитьУведомление Тогда
		Возврат;
	КонецЕсли;	
	
	ПараметрыПисьма = ПолучитьПараметрыПисьма(РезультатФункции);
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("ВажностьЗадачи",Важность);
	ДопПараметры.Вставить("ТекстПисьма",ПараметрыПисьма.ТекстПисьма);
	ДопПараметры.Вставить("ТемаПисьма",ПараметрыПисьма.ТемаПисьма);
	ДопПараметры.Вставить("ПользовательКому",Получатель);
	ДопПараметры.Вставить("Ссылка",СобытияВИстории.Ссылка);
	узОбщийМодульСервер.ОтправитьПисьмо(ДопПараметры);
	
КонецПроцедуры

Процедура ОтправитьУведомлениеНаПочтуРуководителюИнициатора(НастройкиСобытий,СобытияВИстории)
	
	Если Статус = СобытияВИстории.СтарыйСтатус Тогда
		Возврат;	
	КонецЕсли;
	
	Если Статус <> Справочники.узСтатусыЗадачи.НаТестированииПоказПользователям И Статус <> Справочники.узСтатусыЗадачи.Готово Тогда
		Возврат;	
	КонецЕсли; 
	
	Получатель = аэИнициатор.Пользователь.аэРуководитель;
	
	Если НЕ ЗначениеЗаполнено(Получатель) Тогда
		Возврат;
	Конецесли;
	
	Если Получатель.узНеОтправлятьУведомленияНаПочту Тогда
		Возврат;
	Конецесли;
	
	ТекПользователь = Пользователи.ТекущийПользователь(); 
	Если ТекПользователь = Получатель Тогда			
		Возврат;
	Конецесли;	
	ТЗСобытияВИсторииДляУведомлений = СобытияВИстории.ТЗСобытияВИсторииДляУведомлений;
	ВсегоСобытий = ТЗСобытияВИсторииДляУведомлений.Количество();
	Если ВсегоСобытий = 0 Тогда
		Возврат;
	Конецесли;
	
	ВТДопПараметры = Новый Структура();
	ВТДопПараметры.Вставить("ЭтоОтправкаРуководителюИсполнителя",Истина);
	ВТДопПараметры.Вставить("НастройкиСобытий",НастройкиСобытий);
	РезультатФункции = ПолучитьТемаПисьмаСобытие(ВТДопПараметры,СобытияВИстории);
	
	Если НЕ РезультатФункции.НеобходимоОтправитьУведомление Тогда
		Возврат;
	КонецЕсли;	
	
	ПараметрыПисьма = ПолучитьПараметрыПисьма(РезультатФункции);
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("ВажностьЗадачи",Важность);
	ДопПараметры.Вставить("ТекстПисьма",ПараметрыПисьма.ТекстПисьма);
	ДопПараметры.Вставить("ТемаПисьма",ПараметрыПисьма.ТемаПисьма);
	ДопПараметры.Вставить("ПользовательКому",Получатель);
	ДопПараметры.Вставить("Ссылка",СобытияВИстории.Ссылка);
	
	узОбщийМодульСервер.ОтправитьПисьмо(ДопПараметры);
	
КонецПроцедуры

//agroeco-

Функция ПолучитьНастройкиСобытий() 
	РезультатФункции = Новый Структура();
	
	ВидыСобытий_ДобавленаЗадача = ПредопределенноеЗначение("Перечисление.узВидыСобытий.ДобавленаЗадача");
	ВидыСобытий_НовыйИсполнитель = ПредопределенноеЗначение("Перечисление.узВидыСобытий.НовыйИсполнитель");
	ВидыСобытий_ДобавленКомментарий = ПредопределенноеЗначение("Перечисление.узВидыСобытий.ДобавленКомментарий");
	ВидыСобытий_ИзмененКомментарий = ПредопределенноеЗначение("Перечисление.узВидыСобытий.ИзмененКомментарий");
	ВидыСобытий_ИзмененоОписаниеЗадачи = ПредопределенноеЗначение("Перечисление.узВидыСобытий.ИзмененоОписаниеЗадачи");
	ВидыСобытий_ИзменениеСтатуса = ПредопределенноеЗначение("Перечисление.узВидыСобытий.ИзменениеСтатуса");
	
	РезультатФункции.Вставить("ВидыСобытий_ДобавленаЗадача",ВидыСобытий_ДобавленаЗадача);
	РезультатФункции.Вставить("ВидыСобытий_НовыйИсполнитель",ВидыСобытий_НовыйИсполнитель);
	РезультатФункции.Вставить("ВидыСобытий_ДобавленКомментарий",ВидыСобытий_ДобавленКомментарий);
	РезультатФункции.Вставить("ВидыСобытий_ИзмененКомментарий",ВидыСобытий_ИзмененКомментарий);
	РезультатФункции.Вставить("ВидыСобытий_ИзмененоОписаниеЗадачи",ВидыСобытий_ИзмененоОписаниеЗадачи);
	РезультатФункции.Вставить("ВидыСобытий_ИзменениеСтатуса",ВидыСобытий_ИзменениеСтатуса);
	
	МассивСобытийДляНаблюдателяКоторыеПодлежатОтправки = Новый Массив();
	МассивСобытийДляНаблюдателяКоторыеПодлежатОтправки.Добавить(ВидыСобытий_ИзменениеСтатуса);
	МассивСобытийДляНаблюдателяКоторыеПодлежатОтправки.Добавить(ВидыСобытий_НовыйИсполнитель);
	МассивСобытийДляНаблюдателяКоторыеПодлежатОтправки.Добавить(ВидыСобытий_ДобавленКомментарий);
	МассивСобытийДляНаблюдателяКоторыеПодлежатОтправки.Добавить(ВидыСобытий_ИзмененКомментарий);
	
	РезультатФункции.Вставить("МассивСобытийДляНаблюдателяКоторыеПодлежатОтправки",МассивСобытийДляНаблюдателяКоторыеПодлежатОтправки);
	
	МассивСобытийКоторыеПодлежатОтправки = Новый Массив();
	МассивСобытийКоторыеПодлежатОтправки.Добавить(ВидыСобытий_ДобавленаЗадача);
	МассивСобытийКоторыеПодлежатОтправки.Добавить(ВидыСобытий_НовыйИсполнитель);
	МассивСобытийКоторыеПодлежатОтправки.Добавить(ВидыСобытий_ДобавленКомментарий);
	МассивСобытийКоторыеПодлежатОтправки.Добавить(ВидыСобытий_ИзмененКомментарий);
	
	РезультатФункции.Вставить("МассивСобытийКоторыеПодлежатОтправки",МассивСобытийКоторыеПодлежатОтправки);
	
	МассивСобытийДляСтарогоИсполнителяКоторыеПодлежатОтправки = Новый Массив();
	МассивСобытийДляСтарогоИсполнителяКоторыеПодлежатОтправки.Добавить(ВидыСобытий_НовыйИсполнитель);
	РезультатФункции.Вставить("МассивСобытийДляСтарогоИсполнителяКоторыеПодлежатОтправки",МассивСобытийДляСтарогоИсполнителяКоторыеПодлежатОтправки);
	
	Возврат РезультатФункции;	
КонецФункции 

Процедура ОтправитьУведомлениеНаПочтуИсполнителю(НастройкиСобытий,СобытияВИстории)
	Если НЕ ЗначениеЗаполнено(Исполнитель) Тогда
		Возврат;
	Конецесли;
	
	Если Исполнитель.узНеОтправлятьУведомленияНаПочту Тогда
		Возврат;
	Конецесли;
	
	ТекПользователь = Пользователи.ТекущийПользователь(); 
	Если ТекПользователь = Исполнитель Тогда			
		Возврат;
	Конецесли;	
	ТЗСобытияВИсторииДляУведомлений = СобытияВИстории.ТЗСобытияВИсторииДляУведомлений;
	ВсегоСобытий = ТЗСобытияВИсторииДляУведомлений.Количество();
	Если ВсегоСобытий = 0 Тогда
		Возврат;
	Конецесли;
	
	ВТДопПараметры = Новый Структура();
	ВТДопПараметры.Вставить("ЭтоОтправкаИсполнителю",Истина);
	ВТДопПараметры.Вставить("НастройкиСобытий",НастройкиСобытий);
	РезультатФункции = ПолучитьТемаПисьмаСобытие(ВТДопПараметры,СобытияВИстории);
	
	Если НЕ РезультатФункции.НеобходимоОтправитьУведомление Тогда
		Возврат;
	КонецЕсли;	
	
	ПараметрыПисьма = ПолучитьПараметрыПисьма(РезультатФункции);
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("ВажностьЗадачи",Важность);
	ДопПараметры.Вставить("ТекстПисьма",ПараметрыПисьма.ТекстПисьма);
	ДопПараметры.Вставить("ТемаПисьма",ПараметрыПисьма.ТемаПисьма);
	ДопПараметры.Вставить("ПользовательКому",Исполнитель);
	ДопПараметры.Вставить("Ссылка",СобытияВИстории.Ссылка);
	узОбщийМодульСервер.ОтправитьПисьмо(ДопПараметры);			
КонецПроцедуры 

Процедура ОтправитьУведомлениеНаПочтуСтаромуИсполнителю(НастройкиСобытий,СобытияВИстории)
	СтарыйИсполнитель = СобытияВИстории.СтарыйИсполнитель;
	
	Если НЕ ЗначениеЗаполнено(СтарыйИсполнитель) Тогда
		Возврат;
	Конецесли;
	
	Если СтарыйИсполнитель.узНеОтправлятьУведомленияНаПочту Тогда
		Возврат;
	Конецесли;
	
	ТекПользователь = Пользователи.ТекущийПользователь(); 
	Если ТекПользователь = СтарыйИсполнитель Тогда			
		Возврат;
	Конецесли;	
	
	Если Исполнитель = СтарыйИсполнитель Тогда
		Возврат;
	КонецЕсли;
	
	ТЗСобытияВИсторииДляУведомлений = СобытияВИстории.ТЗСобытияВИсторииДляУведомлений;
	ВсегоСобытий = ТЗСобытияВИсторииДляУведомлений.Количество();
	Если ВсегоСобытий = 0 Тогда
		Возврат;
	Конецесли;
	
	ВТДопПараметры = Новый Структура();
	ВТДопПараметры.Вставить("ЭтоОтправкаСтаромуИсполнителю",Истина);
	ВТДопПараметры.Вставить("НастройкиСобытий",НастройкиСобытий);
	РезультатФункции = ПолучитьТемаПисьмаСобытие(ВТДопПараметры,СобытияВИстории);		
		
	Если НЕ РезультатФункции.НеобходимоОтправитьУведомление Тогда
		Возврат;
	КонецЕсли;	
	
	ПараметрыПисьма = ПолучитьПараметрыПисьма(РезультатФункции);
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("ВажностьЗадачи",Важность);
	ДопПараметры.Вставить("ТекстПисьма",ПараметрыПисьма.ТекстПисьма);
	ДопПараметры.Вставить("ТемаПисьма",ПараметрыПисьма.ТемаПисьма);
	ДопПараметры.Вставить("ПользовательКому",СтарыйИсполнитель);
	ДопПараметры.Вставить("Ссылка",СобытияВИстории.Ссылка);
	узОбщийМодульСервер.ОтправитьПисьмо(ДопПараметры);			

КонецПроцедуры 

Процедура ОтправитьУведомлениеНаПочтуНаблюдателям(НастройкиСобытий,СобытияВИстории)
	ТЗСобытияВИсторииДляУведомлений = СобытияВИстории.ТЗСобытияВИсторииДляУведомлений;
	ВсегоСобытий = ТЗСобытияВИсторииДляУведомлений.Количество();
	Если ВсегоСобытий = 0 Тогда
		Возврат;
	Конецесли;
	
	МассивНаблюдателей = ПолучитьМассивНаблюдателей();
	Если МассивНаблюдателей.Количество() = 0 Тогда
		Возврат;
	Конецесли;
	
		
	ВТДопПараметры = Новый Структура();
	ВТДопПараметры.Вставить("ЭтоОтправкаНаблюдателям",Истина);
	ВТДопПараметры.Вставить("НастройкиСобытий",НастройкиСобытий);
	РезультатФункции = ПолучитьТемаПисьмаСобытие(ВТДопПараметры,СобытияВИстории);	
		
	Если НЕ РезультатФункции.НеобходимоОтправитьУведомление Тогда		
		Возврат;
	КонецЕсли;	
	
	ПараметрыПисьма = ПолучитьПараметрыПисьма(РезультатФункции);
	
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("ВажностьЗадачи",Важность);
	ДопПараметры.Вставить("ТекстПисьма",ПараметрыПисьма.ТекстПисьма);
	ДопПараметры.Вставить("ТемаПисьма",ПараметрыПисьма.ТемаПисьма);
	ДопПараметры.Вставить("МассивПользователейКому",МассивНаблюдателей);
	ДопПараметры.Вставить("Ссылка",СобытияВИстории.Ссылка);
	узОбщийМодульСервер.ОтправитьПисьмо(ДопПараметры);			
	
КонецПроцедуры 

Функция ПолучитьМассивНаблюдателей() 
	
	МассивНаблюдателей = Новый Массив();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	узНаблюдателиЗаЗадачами.Пользователь
	|ИЗ
	|	РегистрСведений.узНаблюдателиЗаЗадачами КАК узНаблюдателиЗаЗадачами
	|ГДЕ
	|	узНаблюдателиЗаЗадачами.Задача = &Задача
	|	И узНаблюдателиЗаЗадачами.Пользователь.узНеОтправлятьУведомленияНаПочту = ЛОЖЬ
	|	И узНаблюдателиЗаЗадачами.Пользователь <> &ТекущийПользователь
	|	И узНаблюдателиЗаЗадачами.Пользователь <> &Исполнитель";
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	Запрос.УстановитьПараметр("Исполнитель", Исполнитель);
	Запрос.УстановитьПараметр("Задача", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			МассивНаблюдателей.Добавить(Выборка.Пользователь);
		КонецЦикла;	
	Конецесли;
	
	Для каждого СтрокаНаблюдатели из Статус.Наблюдатели цикл
		Если МассивНаблюдателей.Найти(СтрокаНаблюдатели.Пользователь) <> Неопределено Тогда
			Продолжить;
		Конецесли;
		Если СтрокаНаблюдатели.Пользователь = ПараметрыСеанса.ТекущийПользователь Тогда
			Продолжить;
		Конецесли;
		МассивНаблюдателей.Добавить(СтрокаНаблюдатели.Пользователь);
	Конеццикла;
	
	Возврат МассивНаблюдателей;
	
КонецФункции 

Функция ПолучитьТемаПисьмаСобытие(ДопПараметры,СобытияВИстории) 
	
	ТЗСобытияВИсторииДляУведомлений = СобытияВИстории.ТЗСобытияВИсторииДляУведомлений;
	РезультатФункции = Новый Структура();
	
	НастройкиСобытий = ДопПараметры.НастройкиСобытий;
	
	ЭтоОтправкаИсполнителю = Ложь;
	Если ДопПараметры.Свойство("ЭтоОтправкаИсполнителю") Тогда
		ЭтоОтправкаИсполнителю = Истина;
		МассивСобытийКоторыеПодлежатОтправки = НастройкиСобытий.МассивСобытийКоторыеПодлежатОтправки; 
	КонецЕсли;
	
	ЭтоОтправкаНаблюдателям = Ложь;
	Если ДопПараметры.Свойство("ЭтоОтправкаНаблюдателям") Тогда
		ЭтоОтправкаНаблюдателям = Истина;
		МассивСобытийКоторыеПодлежатОтправки = НастройкиСобытий.МассивСобытийДляНаблюдателяКоторыеПодлежатОтправки; 
	КонецЕсли;
	
	ЭтоОтправкаСтаромуИсполнителю = Ложь;
	Если ДопПараметры.Свойство("ЭтоОтправкаСтаромуИсполнителю") Тогда
		ЭтоОтправкаНаблюдателям = Истина;
		МассивСобытийКоторыеПодлежатОтправки = НастройкиСобытий.МассивСобытийДляСтарогоИсполнителяКоторыеПодлежатОтправки; 
	КонецЕсли;
	
	//agroeco+, КлочковСВ
	ЭтоОтправкаРуководителюИсполнителя = Ложь;
	Если ДопПараметры.Свойство("ЭтоОтправкаРуководителюИсполнителя") Тогда
		ЭтоОтправкаРуководителюИсполнителя = Истина;
		МассивСобытийКоторыеПодлежатОтправки = НастройкиСобытий.МассивСобытийКоторыеПодлежатОтправки; 
	КонецЕсли;
	
	ЭтоОтправкаИнициатору = Ложь;
	Если ДопПараметры.Свойство("ЭтоОтправкаИнициатору") Тогда
		ЭтоОтправкаИнициатору = Истина;
		МассивСобытийКоторыеПодлежатОтправки = НастройкиСобытий.МассивСобытийКоторыеПодлежатОтправки; 
	КонецЕсли;
	
	//agroeco-
	
	НеобходимоОтправитьУведомление = Ложь;	
	ТемаПисьмаСобытие = "";
	МассивИзмененнийПоКомментариям = Новый Массив();
	
	ВсегоСобытий = ТЗСобытияВИсторииДляУведомлений.Количество();
	Если ВсегоСобытий = 1 Тогда
		СтрокаТЗСобытияВИсторииДляУведомлений = ТЗСобытияВИсторииДляУведомлений[0];
		ВидСобытия = СтрокаТЗСобытияВИсторииДляУведомлений.ВидСобытия;
		Если ВидСобытия = НастройкиСобытий.ВидыСобытий_ДобавленаЗадача Тогда
			
			Если ЭтоОтправкаИсполнителю Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Вам была назначена задача:",31);
			КонецЕсли;
			
			//agroeco+, КлочковСВ
			Если ЭтоОтправкаРуководителюИсполнителя Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Создана новая задача задача:",31);
			КонецЕсли;
			
			Если ЭтоОтправкаИнициатору Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Создана новая задача задача:",31);
			КонецЕсли;
			//agroeco-
			
		ИначеЕсли ВидСобытия = НастройкиСобытий.ВидыСобытий_НовыйИсполнитель Тогда
			
			Если ЭтоОтправкаИсполнителю Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Вам была назначена задача:",31);
			КонецЕсли;
			
			//agroeco+, КлочковСВ
			Если ЭтоОтправкаРуководителюИсполнителя Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("В задаче изменен исполнитель:",31);
			КонецЕсли;
			
			Если ЭтоОтправкаИнициатору Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("В задаче изменен исполнитель:",31);
			КонецЕсли;
			//agroeco-
			
			Если ЭтоОтправкаНаблюдателям 
				ИЛИ ЭтоОтправкаСтаромуИсполнителю Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Изменился исполнитель у задачи:",32);
			Конецесли;
			
		ИначеЕсли ВидСобытия = НастройкиСобытий.ВидыСобытий_ДобавленКомментарий Тогда
			Если ЭтоОтправкаИсполнителю
				//agroeco+, КлочковСВ
				ИЛИ ЭтоОтправкаРуководителюИсполнителя
				ИЛИ ЭтоОтправкаИнициатору
				//agroeco-
				ИЛИ ЭтоОтправкаНаблюдателям Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Добален комментарий по задаче:",33);
				МассивИзмененнийПоКомментариям = СтрокаТЗСобытияВИсторииДляУведомлений.МассивИзмененнийПоКомментариям 
			КонецЕсли;
		ИначеЕсли ВидСобытия = НастройкиСобытий.ВидыСобытий_ИзмененКомментарий Тогда
			Если ЭтоОтправкаИсполнителю
				//agroeco+, КлочковСВ
				ИЛИ ЭтоОтправкаРуководителюИсполнителя
				ИЛИ ЭтоОтправкаИнициатору
				//agroeco-
				ИЛИ ЭтоОтправкаНаблюдателям Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Изменен комментарий по задаче:",34);
				МассивИзмененнийПоКомментариям = СтрокаТЗСобытияВИсторииДляУведомлений.МассивИзмененнийПоКомментариям 
			КонецЕсли;
		ИначеЕсли ВидСобытия = НастройкиСобытий.ВидыСобытий_ИзменениеСтатуса Тогда
			
			Если ЭтоОтправкаНаблюдателям Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Изменен статус у задачи:",35);
			Конецесли;
			
			//agroeco+, КлочковСВ
			Если ЭтоОтправкаРуководителюИсполнителя Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Изменен статус у задачи:",35);
			Конецесли;
			
			Если ЭтоОтправкаИнициатору Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Изменен статус у задачи:",35);
			Конецесли;
			//agroeco-
			
		Конецесли;
	Иначе
		Если ТЗСобытияВИсторииДляУведомлений.Найти(НастройкиСобытий.ВидыСобытий_НовыйИсполнитель,"ВидСобытия") <> Неопределено Тогда
			
			Если ЭтоОтправкаИсполнителю Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Вам была назначена задача:",31);
			КонецЕсли;
			
			//agroeco+, КлочковСВ
			Если ЭтоОтправкаРуководителюИсполнителя Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Создана новая задача задача:",31);
			КонецЕсли;
			
			Если ЭтоОтправкаИнициатору Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Создана новая задача задача:",31);
			КонецЕсли;
			//agroeco-
			
			Если ЭтоОтправкаНаблюдателям
				ИЛИ ЭтоОтправкаСтаромуИсполнителю Тогда
				НеобходимоОтправитьУведомление = Истина;
				ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Изменился исполнитель у задачи:",32);
			Конецесли;				
		Иначе
			
			Для каждого СтрокаТЗСобытияВИсторииДляУведомлений из ТЗСобытияВИсторииДляУведомлений цикл
				
				ВидСобытия = СтрокаТЗСобытияВИсторииДляУведомлений.ВидСобытия;
				Если МассивСобытийКоторыеПодлежатОтправки.Найти(ВидСобытия) <> Неопределено Тогда
					НеобходимоОтправитьУведомление = Истина;
					ТемаПисьмаСобытие = узОбщийМодульСервер.ПолучитьТекстСообщения("Изменена задача: ",36);
					Прервать;
				Конецесли;
				
			Конеццикла;	
		Конецесли;
	Конецесли;
	
	РезультатФункции.Вставить("НеобходимоОтправитьУведомление",НеобходимоОтправитьУведомление);
	РезультатФункции.Вставить("ТемаПисьмаСобытие",ТемаПисьмаСобытие);
	РезультатФункции.Вставить("МассивИзмененнийПоКомментариям",МассивИзмененнийПоКомментариям);
	
	Возврат РезультатФункции;
	
КонецФункции 


Функция ПолучитьПараметрыПисьма(ДопПараметры) 
	МассивКодовСообщений = Новый Массив();
	МассивКодовСообщений.Добавить(22); //Добрый день
	МассивКодовСообщений.Добавить(23); //Номер задачи
	МассивКодовСообщений.Добавить(24); //Описание задачи
	МассивКодовСообщений.Добавить(25); //Комментарии
	МассивКодовСообщений.Добавить(26); //Реквизиты задачи
	МассивКодовСообщений.Добавить(27); //Исполнитель
	МассивКодовСообщений.Добавить(28); //Статус
	МассивКодовСообщений.Добавить(29); //Важность
	МассивКодовСообщений.Добавить(30); //ОсновнаяЗадача
	
	СтруктураСообщений = узОбщийМодульСервер.ПолучитьСтруктуруСообщений(МассивКодовСообщений);
	
	ТекстВПисьмеДобрыйДень = СтруктураСообщений.Текст22;
	ТекстВПисьмеНомерЗадачи = СтруктураСообщений.Текст23;
	ТекстВПисьмеОписаниеЗадачи = СтруктураСообщений.Текст24;
	ТекстВПисьмеКомментарий = СтруктураСообщений.Текст25;
	ТекстВПисьмеРеквизитыЗадачи = СтруктураСообщений.Текст26;
	ТекстВПисьмеИсполнитель = СтруктураСообщений.Текст27;
	ТекстВПисьмеСтатус = СтруктураСообщений.Текст28;
	ТекстВПисьмеВажность = СтруктураСообщений.Текст29;
	ТекстВПисьмеОсновнаяЗадача = СтруктураСообщений.Текст30;
	
	ТемаПисьмаСобытие = ДопПараметры.ТемаПисьмаСобытие;
	МассивИзмененнийПоКомментариям = ДопПараметры.МассивИзмененнийПоКомментариям;
	
	РезультатФункции = Новый Структура();
	
	НаименованиеЗадачи = СокрЛП(Наименование);
	НомерЗадачи = ""+Формат(Код,"ЧГ=0");
	ТемаПисьма = "[#"+НомерЗадачи+"] "+ТемаПисьмаСобытие + НаименованиеЗадачи;		
	ТекстПисьма = "
	|"+ТекстВПисьмеДобрыйДень+".
	|
	|"+ТемаПисьмаСобытие+" "+НаименованиеЗадачи+"
	|"+ТекстВПисьмеНомерЗадачи+": #"+НомерЗадачи+"
	|";
	
	Если МассивИзмененнийПоКомментариям.Количество() = 0 Тогда
		Если ЗначениеЗаполнено(ТекстСодержания) Тогда
			ТекстПисьма = ТекстПисьма + "
			|
			|"+ТекстВПисьмеОписаниеЗадачи+": 
			|"+ТекстСодержания+"
			|";			
		Конецесли;	
	Иначе
		ТекстПисьма = ТекстПисьма + "
		|
		|"+ТекстВПисьмеКомментарий+":"; 		
		Для каждого ЭлМассивИзмененнийПоКомментариям из МассивИзмененнийПоКомментариям цикл
			ТекстПисьма = ТекстПисьма + Символы.ПС
				+ " - "+ЭлМассивИзмененнийПоКомментариям;							
		Конеццикла;
	Конецесли;
	
	ТекстПисьма = ТекстПисьма + "
	|
	|"+ТекстВПисьмеРеквизитыЗадачи+":
	|- "+ТекстВПисьмеИсполнитель+" ["+Исполнитель+"]
	|- "+ТекстВПисьмеСтатус+" ["+Статус+"]
	|- "+ТекстВПисьмеВажность+" ["+Важность+"]
	|- "+ТекстВПисьмеОсновнаяЗадача+" ["+ОсновнаяЗадача+"]
	|";			
	
	РезультатФункции.Вставить("ТемаПисьма",ТемаПисьма);
	РезультатФункции.Вставить("ТекстПисьма",ТекстПисьма);
	
	Возврат РезультатФункции;	
КонецФункции 

Функция ЕстьПодчиненныеЗадачи(Задача,ЗадачаРебенок = Неопределено) Экспорт
	пЕстьПодчиненныеЗадачи = Ложь;
	Если ЭтоНовый() Тогда
		Возврат пЕстьПодчиненныеЗадачи;	
	Конецесли;
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	узЗадачи.Ссылка
	|ИЗ
	|	Справочник.узЗадачи КАК узЗадачи
	|ГДЕ
	|	узЗадачи.Ссылка В ИЕРАРХИИ(&Задача)
	|	И узЗадачи.Ссылка <> &Задача
	|	И узЗадачи.Ссылка <> &ЗадачаРебенок
	|");

	Запрос.УстановитьПараметр("Задача", Задача);
	Запрос.УстановитьПараметр("ЗадачаРебенок", ЗадачаРебенок);

	РезультатЗапроса = Запрос.Выполнить(); 
	Если НЕ РезультатЗапроса.Пустой() Тогда
		пЕстьПодчиненныеЗадачи = Истина;
	Конецесли;
	Возврат пЕстьПодчиненныеЗадачи;
КонецФункции

Функция ПолучитьОсновнуюЗадачу(СсылкаНаОбъект)
	//РодителяОбъекта = СсылкаНаОбъект.Родитель;
	//Если ЗначениеЗаполнено(РодителяОбъекта)
	//	И НЕ СсылкаНаОбъект.ЭтоОсновнаяЗадача Тогда
	//	Возврат ПолучитьОсновнуюЗадачу(РодителяОбъекта);
	//Иначе
		Возврат СсылкаНаОбъект;			
	//Конецесли;	
КонецФункции 

Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;	
	КонецЕсли; 
	
	Если ДополнительныеСвойства.Свойство("узЭтоОбработка") Тогда
		Возврат;
	Конецесли;
	
	СобытияВИстории = ДополнительныеСвойства.СобытияВИстории;
	СобытияВИстории.Вставить("Ссылка", Ссылка);
	
	РегистрацияАктивностиПользователя(СобытияВИстории,Отказ);
	ОтправитьУведомлениеНаПочту(СобытияВИстории);
	
	ДополнительныеСвойства.Удалить("СобытияВИстории");
	
	//agroeco+ miv
	ЗарегистрироватьВзаимодействие();
	ПроверитьПроект(Отказ);
	
	ПроверитьЭлектроннуюПочтуИнициатораИРуководителя(Отказ);
	//agroeco- miv
КонецПроцедуры

Процедура ПроверитьЭлектроннуюПочтуИнициатораИРуководителя(Отказ)
	
	Если Не ЗначениеЗаполнено(аэИнициатор.Пользователь) Тогда
		
		Сообщить("У инициатора не заполнен пользователь!");
		Отказ = Истина;	
		Возврат;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(аэИнициатор.Пользователь.аэРуководитель) Тогда
		
		Сообщить("У инициатора не заполнен руководитель!");
		Отказ = Истина;	
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", аэИнициатор.Пользователь);
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПользователиКонтактнаяИнформация.АдресЭП
	|ИЗ
	|	Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
	|ГДЕ
	|	ПользователиКонтактнаяИнформация.Ссылка = &Ссылка
	|	И ПользователиКонтактнаяИнформация.Тип = &Тип"
	;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Если Не ЗначениеЗаполнено(Выборка.АдресЭП) Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
		
	Иначе
		
		Сообщить("У инициатора не настроена электронная почта");
		Отказ = Истина;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", аэИнициатор.Пользователь.аэРуководитель);
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПользователиКонтактнаяИнформация.АдресЭП
	|ИЗ
	|	Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
	|ГДЕ
	|	ПользователиКонтактнаяИнформация.Ссылка = &Ссылка
	|	И ПользователиКонтактнаяИнформация.Тип = &Тип"
	;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Если Не ЗначениеЗаполнено(Выборка.АдресЭП) Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
		
	Иначе
		
		Сообщить("У руководителя инициатора не настроена электронная почта");
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьВзаимодействие()
	
	Если Не ЗначениеЗаполнено(Предмет) Тогда
		Возврат;
	КонецЕсли;
	
	//Регистрация взаимодействия
	НаборЗаписей = РегистрыСведений.ПредметыПапкиВзаимодействий.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Взаимодействие.Установить(Ссылка);
	
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Взаимодействие = Ссылка;
	НоваяЗапись.Предмет = Предмет;
	НоваяЗапись.ПапкаЭлектронногоПисьма = Справочники.ПапкиЭлектронныхПисем.НайтиПоНаименованию("Входящие");
	
	НаборЗаписей.Записать();
	
	//Запись информации о взаимодействиях в справочник
	НайденноеВзаимодействие = Справочники.аэСтруктураВзаимодействий.НайтиПоРеквизиту("Взаимодействие", Ссылка);
	ВзаимодействиеЗаполнено = ЗначениеЗаполнено(НайденноеВзаимодействие);
	
	Если Не ВзаимодействиеЗаполнено Тогда
		
		НовыйЭлемент = Справочники.аэСтруктураВзаимодействий.СоздатьЭлемент();
		НовыйЭлемент.Взаимодействие = Ссылка;
		НовыйЭлемент.Дата = Ссылка.ДатаСоздания;
		НовыйЭлемент.Записать();
		
		НайденноеВзаимодействие = НовыйЭлемент.Ссылка;
		
	КонецЕсли;
	
	//Если ЗначениеЗаполнено(Предмет) Тогда
	//	
	//	Если НайденноеВзаимодействие.Родитель.Взаимодействие <> Предмет Тогда
	//		
	//		ВзаимодействиеПоПредмету = Справочники.аэСтруктураВзаимодействий.НайтиПоРеквизиту("Взаимодействие", Предмет);
	//		
	//		Если Не ЗначениеЗаполнено(ВзаимодействиеПоПредмету) Тогда
	//			
	//			НовыйЭлемент = Справочники.аэСтруктураВзаимодействий.СоздатьЭлемент();
	//			НовыйЭлемент.Взаимодействие = Предмет;
	//			НовыйЭлемент.Записать();
	//			
	//			ВзаимодействиеПоПредмету = НовыйЭлемент.Ссылка;
	//			
	//		КонецЕсли;
	//		
	//		ТекЭлемент = НайденноеВзаимодействие.ПолучитьОбъект();
	//		ТекЭлемент.Родитель = ВзаимодействиеПоПредмету;
	//		ТекЭлемент.Записать();
	//		
	//	КонецЕсли;
	//	
	//КонецЕсли;
	
	
КонецПроцедуры

Процедура ПроверитьПроект(Отказ)
	
	Если Не Проект.Активный Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Проект %1 не активен. Изменение задачи невозможно!'"), 
			Проект);
		Сообщить(ТекстСообщения);
		
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура РегистрацияАктивностиПользователя(СобытияВИстории,Отказ)
	ВТДопПараметры = Новый Структура();
	ВТДопПараметры.Вставить("СобытияВИстории",СобытияВИстории);
	РегистрыСведений.узАктивностиПользователей.ДобавитьАктивностьПользователя(ЭтотОбъект,ВТДопПараметры,Отказ);
КонецПроцедуры 

Процедура ИзменитьОсновнуюЗадачуДляВсехПодчиненныхЗадач()
	Если ЭтоНовый() Тогда
		Возврат;	
	Конецесли;
		
	пОсновнаяЗадача = ОсновнаяЗадача;
	Если ЭтоОсновнаяЗадача Тогда
		пОсновнаяЗадача = Ссылка;
	Конецесли;

	Если НЕ ЗначениеЗаполнено(пОсновнаяЗадача) Тогда
		Возврат;
	КонецЕсли;	
	
	МассивПодчиненныхЗадач = Новый Массив();
	ЗаполнитьМассивПодчиненныхЗадач(Ссылка,пОсновнаяЗадача,МассивПодчиненныхЗадач);
	
	Для каждого пЗадача из МассивПодчиненныхЗадач цикл
		СпрОбъект = пЗадача.ПолучитьОбъект();
		#Если Тромбон тогда
			СпрОбъект = Справочники.узЗадачи.СоздатьЭлемент();
		#Конецесли
		
		СпрОбъект.ОсновнаяЗадача = пОсновнаяЗадача;
		СпрОбъект.ДополнительныеСвойства.Вставить("узЭтоОбработка",Истина);
		СпрОбъект.Записать();
	Конеццикла;
	
КонецПроцедуры 

Процедура ЗаполнитьМассивПодчиненныхЗадач(ЗадачаРодитель,пОсновнаяЗадача,МассивПодчиненныхЗадач) 
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	узЗадачи.Ссылка
	|ИЗ
	|	Справочник.узЗадачи КАК узЗадачи
	|ГДЕ
	|	узЗадачи.Родитель = &ЗадачаРодитель
	|	И узЗадачи.Ссылка <> &ЗадачаРодитель
	|");

	Запрос.УстановитьПараметр("ЗадачаРодитель", ЗадачаРодитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	Конецесли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		пЗадача = Выборка.Ссылка;
		
		МассивПодчиненныхЗадач.Добавить(пЗадача);
		
		Если пЗадача.ЭтоОсновнаяЗадача Тогда
			Продолжить;	
		Конецесли;
		
		Если пЗадача.ОсновнаяЗадача = пОсновнаяЗадача Тогда
			Продолжить;
		Конецесли;
		
		ЗаполнитьМассивПодчиненныхЗадач(пЗадача,пОсновнаяЗадача,МассивПодчиненныхЗадач);
	Конеццикла;
	
КонецПроцедуры 

Функция ПроверитьWIPЛимит() Экспорт
	ПревышенWIPЛимит = Ложь;
	ТекстОшибки = "";
	
	РезультатФункции = Новый Структура();
	РезультатФункции.Вставить("ПревышенWIPЛимит",ПревышенWIPЛимит);
	РезультатФункции.Вставить("ТекстОшибки",ТекстОшибки);
	
	Если НЕ ЗначениеЗаполнено(Статус) Тогда
		Возврат РезультатФункции;
	Конецесли;
	
	Если НЕ Статус.ИспользоватьWIPЛимит Тогда
		Возврат РезультатФункции;
	Конецесли;
	
	пWIPЛимит = Справочники.узСтатусыЗадачи.ПолучитьWIPЛимит(Статус);
	
	пКоличествоЗадачВСтатусе = ПолучитьКоличествоЗадачВСтатусе();
	
	Если пКоличествоЗадачВСтатусе > пWIPЛимит Тогда
		
		ПревышенWIPЛимит = Истина;
			
		ТекстОшибки = узОбщийМодульСервер.ПолучитьТекстСообщения("Превышен WIP лимит. Количество задач: [%1] WIP лимит: [%2]",88);
		ТекстОшибки = СтрШаблон(ТекстОшибки,пКоличествоЗадачВСтатусе,пWIPЛимит);		
	Конецесли;
	
	РезультатФункции.Вставить("ПревышенWIPЛимит",ПревышенWIPЛимит);
	РезультатФункции.Вставить("ТекстОшибки",ТекстОшибки);
	
	Возврат РезультатФункции;
КонецФункции

Функция ПолучитьКоличествоЗадачВСтатусе() 
	пКоличествоЗадачВСтатусе = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ узЗадачи.Ссылка) КАК КоличествоЗадач
	|ИЗ
	|	Справочник.узЗадачи КАК узЗадачи
	|ГДЕ
	|	узЗадачи.Статус В(&МассивСтатусов)
	|	И ВЫБОР
	|			КОГДА &ИспользоватьWIPЛимитНаИсполнителя
	|				ТОГДА узЗадачи.Исполнитель = &Исполнитель
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И узЗадачи.ПоказыватьВОтчетахИКанбанДоске
	|	И узЗадачи.Ссылка <> &Ссылка";
	
	МассивСтатусов = Новый Массив();
	
	Если Статус.WIPЛимитУказываетсяВРодителе Тогда
		ДобавитьСтатусыСТакимЖеРодителем(МассивСтатусов);
	Иначе
		МассивСтатусов.Добавить(Статус);
	Конецесли;
	
	Запрос.УстановитьПараметр("ИспользоватьWIPЛимитНаИсполнителя", Статус.ИспользоватьWIPЛимитНаИсполнителя);
	Запрос.УстановитьПараметр("Исполнитель", Исполнитель);
	Запрос.УстановитьПараметр("МассивСтатусов", МассивСтатусов);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		пКоличествоЗадачВСтатусе = Выборка.КоличествоЗадач;
	КонецЦикла;
	
	пКоличествоЗадачВСтатусе = пКоличествоЗадачВСтатусе + 1;
	
	Возврат пКоличествоЗадачВСтатусе;
КонецФункции 

Процедура ДобавитьСтатусыСТакимЖеРодителем(МассивСтатусов)
	пРодитель = Статус.Родитель;
	Если НЕ ЗначениеЗаполнено(пРодитель) Тогда
		МассивСтатусов.Добавить(Статус);	
		Возврат;
	Конецесли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	узСтатусыЗадачи.Ссылка
	|ИЗ
	|	Справочник.узСтатусыЗадачи КАК узСтатусыЗадачи
	|ГДЕ
	|	узСтатусыЗадачи.Родитель = &Родитель";
	
	Запрос.УстановитьПараметр("Родитель", пРодитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	Конецесли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МассивСтатусов.Добавить(Выборка.Ссылка);	
	КонецЦикла;
КонецПроцедуры 

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее") Тогда
		
		ЭтотОбъект.Наименование = ДанныеЗаполнения.Тема;
		ЭтотОбъект.Предмет = ДанныеЗаполнения;
		ЭтотОбъект.ПредметСтрокой = ОбщегоНазначения.ПредметСтрокой(ЭтотОбъект.Предмет);
		ЭтотОбъект.Статус = Справочники.узСтатусыЗадачи.НайтиПоНаименованию("Новая задача");
		ЭтотОбъект.ВзаимодействиеОснование = ДанныеЗаполнения;
		ЭтотОбъект.аэИнициатор = ДанныеЗаполнения.ОтправительКонтакт;
		Если Не ЗначениеЗаполнено(ЭтотОбъект.аэИнициатор) Тогда
			ОтправительПредставление = ДанныеЗаполнения.ОтправительПредставление;
			Поз = Найти(ОтправительПредставление, "<");
			Если Поз > 0 Тогда
				ОтправительПредставление = Лев(ОтправительПредставление, Поз - 1);
				ЭтотОбъект.аэИнициатор = Справочники.Пользователи.НайтиПоНаименованию(ОтправительПредставление);
			КонецЕсли;
		КонецЕсли;
		ЭтотОбъект.Исполнитель = ПараметрыСеанса.ТекущийПользователь;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.узЗадачи") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, , "Владелец, Код"); 
		ЭтотОбъект.Предмет = ДанныеЗаполнения;
		//ЭтотОбъект.Родитель = ДанныеЗаполнения.Ссылка;
		ЭтотОбъект.Статус = Справочники.узСтатусыЗадачи.НайтиПоНаименованию("Новая задача");
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ТелефонныйЗвонок") Тогда
		
		ЭтотОбъект.Наименование = ДанныеЗаполнения.Тема;
		ЭтотОбъект.Предмет = ДанныеЗаполнения;
		ЭтотОбъект.ПредметСтрокой = ОбщегоНазначения.ПредметСтрокой(ЭтотОбъект.Предмет);
		ЭтотОбъект.Статус = Справочники.узСтатусыЗадачи.НайтиПоНаименованию("Новая задача");
		ЭтотОбъект.ВзаимодействиеОснование = ДанныеЗаполнения;
		
	КонецЕсли;
КонецПроцедуры
