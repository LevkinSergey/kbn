#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьЗадачи() Экспорт
	пНастройкиКомпоновщика = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);	
	
	ТЗЗадачи = ПолучитьТЗЗадачи(пНастройкиКомпоновщика);
	Для каждого СтрокаТЗЗадачи из ТЗЗадачи цикл
		СтрокаТЗЗадачи.ОсновнаяЗадачаПредставление = СокрЛП(СтрокаТЗЗадачи.ОсновнаяЗадачаПредставление);		
	Конеццикла;
	ТЧЗадачи.Загрузить(ТЗЗадачи);
КонецПроцедуры 

Функция ПолучитьТЗЗадачи(НастройкиКомпоновщика) Экспорт 
	ТЗЗадачи = Новый ТаблицаЗначений;
	СхемаКомпоновкиДанныхКонсоли = ПолучитьМакет("СхемаКомпоновкиДанных");
	
	ИсполняемыеНастройки = НастройкиКомпоновщика;
	
	СписокВыбранныхСтатусов = Новый СписокЗначений;
	Для каждого СтрокаТЧНастройкиКолонок из ТЧНастройкиКолонок цикл
		Если НЕ СтрокаТЧНастройкиКолонок.Видимость Тогда
			Продолжить;
		Конецесли;
		СписокВыбранныхСтатусов.Добавить(СтрокаТЧНастройкиКолонок.Статус);		
	Конеццикла;

	ЗначениеПараметра = ИсполняемыеНастройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Наблюдатель"));
	Если ЗначениеПараметра <> Неопределено Тогда
		ЗначениеПараметра.Использование = Истина;
		ЗначениеПараметра.Значение = Наблюдатель;
	Конецесли;	
	
	ЗначениеПараметра = ИсполняемыеНастройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ИспользоватьОтборПоНаблюдателю"));
	Если ЗначениеПараметра <> Неопределено Тогда
		ЗначениеПараметра.Значение = ЗначениеЗаполнено(Наблюдатель);
		ЗначениеПараметра.Использование=Истина;	
	Конецесли;
	
	ЗначениеПараметра = ИсполняемыеНастройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ТекущаяДата"));
	Если ЗначениеПараметра <> Неопределено Тогда
		ЗначениеПараметра.Значение = ТекущаяДата();
		ЗначениеПараметра.Использование=Истина;	
	Конецесли;
	
	
	МассивВыбранныхСтатусовКолонок = Новый Массив();
	Для каждого СтрокаТЧНастройкиКолонок из ТЧНастройкиКолонок цикл
		Если НЕ СтрокаТЧНастройкиКолонок.Видимость Тогда
			Продолжить;
		Конецесли;
		МассивВыбранныхСтатусовКолонок.Добавить(СтрокаТЧНастройкиКолонок.Статус);
	Конеццикла;
	
	ЗначениеПараметра = ИсполняемыеНастройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("МассивВыбранныхСтатусовКолонок"));
	Если ЗначениеПараметра <> Неопределено Тогда
		ЗначениеПараметра.Значение = МассивВыбранныхСтатусовКолонок;
		ЗначениеПараметра.Использование=Истина;	
	Конецесли;	
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанныхКонсоли, ИсполняемыеНастройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	ПроцессорВывода.УстановитьОбъект(ТЗЗадачи);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);	
	
	Возврат ТЗЗадачи;
КонецФункции 

Процедура СменитьСтатусЗадачи(ДопПараметры) Экспорт
	НовыйСтатус = ДопПараметры.НовыйСтатус;
	МассивЗадач = ДопПараметры.МассивЗадач;
	
	Для каждого ЭлМассиваЗадач из МассивЗадач цикл
		СпрОбъект = ЭлМассиваЗадач.ПолучитьОбъект();		
		СпрОбъект.Статус = НовыйСтатус;
		СпрОбъект.Записать();
	Конеццикла;	
	ЗаполнитьЗадачи();
КонецПроцедуры 

Процедура ЗаполнитьТЧНастройкиКолонок() Экспорт
	
	пРодитель = ПредопределенноеЗначение("Справочник.узСтатусыЗадачи.ПустаяСсылка");
	ЗагрузитьПодчиненныеЭлементы(пРодитель);
	
КонецПроцедуры 

Процедура ЗагрузитьПодчиненныеЭлементы(пРодитель)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	узСтатусыЗадачи.Ссылка КАК Статус,
	|	узСтатусыЗадачи.ВидимостьПоУмолчанию КАК Видимость,
	|	узСтатусыЗадачи.ИмяПредопределенныхДанных
	|ИЗ
	|	Справочник.узСтатусыЗадачи КАК узСтатусыЗадачи
	|ГДЕ
	|	узСтатусыЗадачи.НеИспользуется = ЛОЖЬ
	|	И узСтатусыЗадачи.Родитель = &Родитель
	|
	|УПОРЯДОЧИТЬ ПО
	|	узСтатусыЗадачи.РеквизитДопУпорядочивания";
	
	Запрос.УстановитьПараметр("Родитель",пРодитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	Конецесли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаТЧНастройкиКолонок = ТЧНастройкиКолонок.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧНастройкиКолонок,Выборка);
		
		ЗагрузитьПодчиненныеЭлементы(Выборка.Статус);
	Конеццикла;
	
	
КонецПроцедуры 

#КонецОбласти

#Область ТекущиеДела
Процедура ЗаполнитьТекущиеДела() Экспорт
	ТЧТекущиеДела.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	узТекущиеДела.Ссылка КАК ТекущееДело,
	|	узТекущиеДела.Выполнено КАК Выполнено,
	|	узТекущиеДела.ТекстСодержания,
	|	узТекущиеДела.Автор,
	|	узТекущиеДела.ДатаТекущегоДела,
	|	узТекущиеДела.ДатаСоздания,
	|	узТекущиеДела.ДатаВыполнения,
	|	узТекущиеДела.Задача,
	|	узТекущиеДела.Вопрос,
	|	узТекущиеДела.Порядок КАК Порядок
	|ИЗ
	|	Справочник.узТекущиеДела КАК узТекущиеДела
	|ГДЕ
	|	узТекущиеДела.Автор = &Автор
	|	И ВЫБОР
	|			КОГДА узТекущиеДела.ДатаВыполнения = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ВЫБОР
	|						КОГДА НАЧАЛОПЕРИОДА(узТекущиеДела.ДатаТекущегоДела, ДЕНЬ) <= &НаДату
	|							ТОГДА ИСТИНА
	|						ИНАЧЕ ЛОЖЬ
	|					КОНЕЦ
	//|			КОГДА НАЧАЛОПЕРИОДА(узТекущиеДела.ДатаВыполнения, ДЕНЬ) = &НаДату
	|			КОГДА НАЧАЛОПЕРИОДА(узТекущиеДела.ДатаВыполнения, ДЕНЬ) >= &НаДату
	|				И НАЧАЛОПЕРИОДА(узТекущиеДела.ДатаТекущегоДела, ДЕНЬ) <= &НаДату
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Выполнено,
	|	Порядок
	|";
	
	Запрос.УстановитьПараметр("Автор", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗРезультат = РезультатЗапроса.Выгрузить();
	ТЗРезультат.Колонки.Добавить("ПорядокДоп",Новый ОписаниеТипов("Число"));
	Для каждого СтрокаТЗРезультат из ТЗРезультат цикл
		пПорядокДоп = ПолучитьПорядоДоп(СтрокаТЗРезультат.Выполнено,СтрокаТЗРезультат.ДатаВыполнения,НаДату);	
		СтрокаТЗРезультат.ПорядокДоп = пПорядокДоп;	
	Конеццикла;
	
	ТЗРезультат.Сортировать("ПорядокДоп,Порядок");
	
	Для каждого СтрокаТЗРезультат из ТЗРезультат цикл
		СтрокаТЧТекущиеДела = ТЧТекущиеДела.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧТекущиеДела,СтрокаТЗРезультат);
		СтрокаТЧТекущиеДела.НомерСтрокиОбработка = СтрокаТЧТекущиеДела.НомерСтроки;
	КонецЦикла;
КонецПроцедуры 

Функция ПолучитьПорядоДоп(пВыполнено,пДатаВыполнения,пНаДату) Экспорт
	//Такая же функция есть в форме обработки
	пПорядокДоп = 10;
	Если НЕ пВыполнено Тогда
		пПорядокДоп = 0;
	Иначе
		Если НачалоДня(пДатаВыполнения) <> пНаДату Тогда
			пПорядокДоп = 1;
		Иначе
			пПорядокДоп = 2;
		Конецесли;
	Конецесли;
	Возврат пПорядокДоп;	
КонецФункции 

Процедура СохранитьТекущиеДела() Экспорт
	//ЭтоСегодня = НачалоДня(ТекущаяДата()) = НаДату;
	//Если ЭтоСегодня Тогда
		Для каждого СтрокаТЧТекущиеДела из ТЧТекущиеДела цикл
			СтрокаТЧТекущиеДела.НомерСтрокиОбработка = СтрокаТЧТекущиеДела.НомерСтроки;		
		Конеццикла;
		ТЧТекущиеДела.Сортировать("Выполнено,НомерСтрокиОбработка");
	//КонецЕсли;
	
	Для каждого СтрокаТЧТекущиеДела из ТЧТекущиеДела цикл
		СтрокаТЧТекущиеДела.ТекстСодержания = СокрЛП(СтрокаТЧТекущиеДела.ТекстСодержания);
		Если НЕ ЗначениеЗаполнено(СтрокаТЧТекущиеДела.ТекстСодержания) Тогда
			Продолжить;
		Конецесли;
		
		ТекущееДелоОбъект = ПолучитьТекущееДелоОбъект(СтрокаТЧТекущиеДела); 
		ЗаполнитьЗначенияСвойств(ТекущееДелоОбъект,СтрокаТЧТекущиеДела,,"Автор,ДатаСоздания,Порядок");
		ТекущееДелоОбъект.Наименование = ТекущееДелоОбъект.ТекстСодержания;		
		//Если ЭтоСегодня Тогда
			ТекущееДелоОбъект.Порядок = СтрокаТЧТекущиеДела.НомерСтроки;
		//Конецесли;
		Если СтрокаТЧТекущиеДела.Выполнено = Ложь Тогда
			ТекущееДелоОбъект.ДатаВыполнения = Дата(1,1,1);
		Конецесли;
		Если НЕ ЗначениеЗаполнено(ТекущееДелоОбъект.ДатаТекущегоДела) Тогда
			ТекущееДелоОбъект.ДатаТекущегоДела = ТекущееДелоОбъект.ДатаСоздания;	
		Конецесли;
		
		ТекущееДелоОбъект.Записать();
	Конеццикла;
КонецПроцедуры 

Функция ПолучитьТекущееДелоОбъект(СтрокаТЧТекущиеДела) 
	Перем ТекущееДелоОбъект;
	
	пТекущееДело = СтрокаТЧТекущиеДела.ТекущееДело;
	
	Если ЗначениеЗаполнено(пТекущееДело) Тогда
		ТекущееДелоОбъект = пТекущееДело.ПолучитьОбъект();
		Возврат ТекущееДелоОбъект;		
	Конецесли;
	
	ТекущееДелоОбъект = СоздатьТекущееДело(СтрокаТЧТекущиеДела);
	
	Возврат ТекущееДелоОбъект;
КонецФункции 

Функция СоздатьТекущееДело(СтрокаТЧТекущиеДела) 
	ТекущееДелоОбъект = Справочники.узТекущиеДела.СоздатьЭлемент();
	ТекущееДелоОбъект.Автор = ПараметрыСеанса.ТекущийПользователь;
	ТекущееДелоОбъект.ДатаСоздания = ТекущаяДата();
	ТекущееДелоОбъект.ДатаТекущегоДела = ТекущееДелоОбъект.ДатаСоздания;
	
	Возврат ТекущееДелоОбъект;
КонецФункции 

Процедура УбратьТекущееДело(МассивТекущихДел) Экспорт
	Для каждого пТекущееДело из МассивТекущихДел цикл
		Если НЕ ЗначениеЗаполнено(пТекущееДело) Тогда
			Продолжить;
		Конецесли;
		ТекущееДелоОбъект = пТекущееДело.ПолучитьОбъект();
		ТекущееДелоОбъект.Удалить();			
	Конеццикла;	
КонецПроцедуры 

Процедура ОбновитьНаСервере() Экспорт
	СохранитьТекущиеДела();
	ЗаполнитьТекущиеДела();
КонецПроцедуры 
#КонецОбласти
